pkgname=dwm-git
_pkgname=dwm
pkgver=6.3.r2.gbece862
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft')
makedepends=('git')
provides=('dwm')
conflicts=('dwm')
_commit=bece862
source=("$_pkgname::git+http://git.suckless.org/dwm#commit=$_commit"
        config.h)
md5sums=('SKIP'
         'SKIP') # so you can customize config.h

pkgver(){
  cd $_pkgname
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  patch --directory="$srcdir/dwm/" --forward --strip=1 --input="../../01-dwm-pertag-6.2.diff"
  patch --directory="$srcdir/dwm/" --forward --strip=1 --input="../../02-dwm-fullgaps-20200508-7b77734.diff"
  patch --directory="$srcdir/dwm/" --forward --strip=1 --input="../../03-dwm-alwayscenter-20200625-f04cac6.diff"
  patch --directory="$srcdir/dwm/" --forward --strip=1 --input="../../04-bottomstack-20160719-56a31dc.diff"
  patch --directory="$srcdir/dwm/" --forward --strip=1 --input="../../05-dwm-statuscolors-20220322-bece862.diff"

  cd $_pkgname
  if [[ -f "$srcdir/config.h" ]]; then
    cp -fv "$srcdir/config.h" config.h
  fi
}

build() {
  cd $_pkgname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
